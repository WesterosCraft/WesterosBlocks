buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

// define the properties file
ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.WesterosBlocks_version}${config.WesterosBlocks_extversion}"
group= "com.westeroscraft" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "WesterosBlocks"
targetCompatibility = '1.8'
sourceCompatibility = '1.8'
ext.buildNumber = System.getenv().BUILD_NUMBER ?: "Dev"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
	runDir = "run"
	mappings = config.mappings_version
	replaceIn "Version.java"
    	replace "@VERSION@", config.WesterosBlocks_version + config.WesterosBlocks_extversion + '-' + project.ext.buildNumber
    	replace "@BUILD_NUMBER@", project.ext.buildNumber
}

repositories {
    mavenLocal()
    maven {
        name = "dynmap"
        url = "http://repo.mikeprimm.com"
    }
}

configurations {
    westerosblocks
    compile.extendsFrom westerosblocks
}

dependencies {
    westerosblocks group: 'us.dynmap', name:'DynmapCoreAPI', version: "${config.Dynmap_version}", ext: 'jar'
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':config.WesterosBlocks_version + config.WesterosBlocks_extversion + '-' + project.ext.buildNumber, 'mcversion':config.minecraft_version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    // Move access transformer to META-INF
    //rename '(.+_at.cfg)', 'META-INF/$1'
    
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'com.westeroscraft.westerosblocks.asm.FMLLoadingPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
   classifier = "forge-" + config.minecraft_version
   destinationDir = project.projectDir
   from { configurations.westerosblocks.collect { it.isDirectory() ? it : zipTree(it) } }
   manifest {
        attributes 'FMLAT': 'WesterosBlocks_at.cfg'
   }
}
